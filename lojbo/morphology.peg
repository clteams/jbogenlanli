# This is a Parsing Expression Grammar for the morphology of Lojban.%%%
# See [http://www.pdos.lcs.mit.edu/~baford/packrat/] %%%
# mi'e ((xorxes))%%%
#___________________________________________________________________%%%
words <- pause? (word pause?)*
#word <- lojban_word / non_lojban_word
#lojban_word <- CMENE_ / cmavo / brivla
word <- lojban_word {printf(" - lojban word\n");}
      / non_lojban_word {printf(" - nlw\n");}
lojban_word <- <CMENE_> {printf("cmene: %s",$1);}
             / <cmavo> {printf("cmavo: %s",$2);}
	     / <BRIVLA_> {printf("brivla: %s",$3);}
BRIVLA_ <- gismu / fuhivla / lujvo
#___________________________________________________________________
CMENE_ <- jbocme / zifcme
zifcme <- !h (nucleus / glide / h / C !pause / digit)* C &pause
jbocme <- &zifcme (any_syllable / digit)* &pause 
#___________________________________________________________________
cmavo <- (glide nucleus / C? (vowel/diphthong) (h (vowel/diphthong))?) !cluster
#cmavo <- !CMENE_ !CVCy_lujvo cmavo_form &post_word
#CVCy_lujvo <- CVC_rafsi y h? initial_rafsi* brivla_core / stressed_CVC_rafsi y short_final_rafsi
#cmavo_form <- !h !cluster onset (nucleus h)* (!stressed nucleus / nucleus !cluster) / y+ / digit
#___________________________________________________________________
lujvo <- !gismu !fuhivla !cmavo initial_rafsi* brivla_core
brivla_core <- fuhivla / gismu / CVV_final_rafsi / stressed_initial_rafsi short_final_rafsi 
stressed_initial_rafsi <- stressed_extended_rafsi / stressed_y_rafsi / stressed_y_less_rafsi
initial_rafsi <- extended_rafsi / y_rafsi / !any_extended_rafsi y_less_rafsi !any_extended_rafsi
any_extended_rafsi <- fuhivla / extended_rafsi / stressed_extended_rafsi
#___________________________________________________________________
fuhivla <- fuhivla_head stressed_syllable CS* final_syllable
stressed_extended_rafsi <- stressed_brivla_rafsi / stressed_fuhivla_rafsi 
extended_rafsi <- brivla_rafsi / fuhivla_rafsi
stressed_brivla_rafsi <- &unstressed_syllable brivla_head stressed_syllable h y
brivla_rafsi <- &(syllable CS* syllable) brivla_head h y h?
stressed_fuhivla_rafsi <- fuhivla_head stressed_syllable !h onset y 
fuhivla_rafsi <- &unstressed_syllable fuhivla_head !h onset y h?
fuhivla_head <- !rafsi_string brivla_head
brivla_head <- !cmavo !slinkuhi !h &onset unstressed_syllable*
slinkuhi <- !rafsi_string C rafsi_string 
rafsi_string <- y_less_rafsi* (gismu / CVV_final_rafsi / stressed_y_less_rafsi short_final_rafsi / y_rafsi / stressed_y_rafsi / stressed_y_less_rafsi? initial_pair y / hy_rafsi / stressed_hy_rafsi)
#___________________________________________________________________
gismu <- (initial_pair stressed_vowel / C stressed_vowel C) &final_syllable C vowel &post_word
CVV_final_rafsi <- C stressed_vowel h &final_syllable vowel &post_word
short_final_rafsi <- &final_syllable (C diphthong / initial_pair vowel) &post_word
stressed_hy_rafsi <- (long_rafsi stressed_vowel / stressed_CCV_rafsi / stressed_CVV_rafsi) h y
stressed_y_rafsi <- (stressed_long_rafsi / stressed_CVC_rafsi) y
stressed_y_less_rafsi <- stressed_CVC_rafsi !y / stressed_CCV_rafsi / stressed_CVV_rafsi
stressed_long_rafsi <- initial_pair stressed_vowel C / C stressed_vowel C C 
stressed_CVC_rafsi <- C stressed_vowel C 
stressed_CCV_rafsi <- initial_pair stressed_vowel 
stressed_CVV_rafsi <- C (unstressed_vowel h stressed_vowel / stressed_diphthong) r_hyphen
hy_rafsi <- (long_rafsi vowel / CCV_rafsi / CVV_rafsi) h y h?
y_rafsi <- (long_rafsi / CVC_rafsi) y h?
y_less_rafsi <- !y_rafsi !stressed_y_rafsi !hy_rafsi !stressed_hy_rafsi (CVC_rafsi / CCV_rafsi / CVV_rafsi) !h 
long_rafsi <- initial_pair unstressed_vowel C / C unstressed_vowel C C 
CVC_rafsi <- C unstressed_vowel C
CCV_rafsi <- initial_pair unstressed_vowel
CVV_rafsi <- C (unstressed_vowel h unstressed_vowel / unstressed_diphthong) r_hyphen
r_hyphen <- r &C / n &r
#___________________________________________________________________
final_syllable <-  onset !y !stressed nucleus !CMENE_ &post_word
stressed_syllable <- &stressed syllable / syllable &stress
stressed_diphthong <- &stressed diphthong / diphthong &stress
stressed_vowel <- &stressed vowel / vowel &stress
unstressed_syllable <- !stressed syllable !stress / CS
unstressed_diphthong <- !stressed diphthong !stress
unstressed_vowel <- !stressed vowel !stress
stress <- C* h? y? syllable pause
stressed <- onset comma* [AEIOU]
any_syllable <- onset nucleus coda? / CS 
syllable <- onset !y nucleus coda?
CS <- C &syllabic coda
coda <- !any_syllable C &any_syllable / syllabic? C? &pause 
onset <-  h / glide / initial
nucleus <- vowel / diphthong / y !nucleus
#_________________________________________________________________
glide <- (i / u) &nucleus
diphthong <- (a i !i / a u !u / e i !i / o i !i) !nucleus
vowel <- (a / e / i / o / u) !nucleus
a <- comma* [aA] 
e <- comma* [eE] 
i <- comma* [iI] 
o <- comma* [oO] 
u <- comma* [uU] 
y <- comma* [yY] 
#___________________________________________________________________
cluster <- C C+
initial_pair <- &initial C C !C
initial <- (affricate / sibilant? other? liquid?) !C !glide
affricate <- t c / t s / d j / d z
liquid <- l / r 
other <- p / t !l / k / f / x / b / d !l / g / v / m / n !liquid 
sibilant <- c / s !x / (j / z) !n !liquid
C <- voiced / unvoiced / syllabic
syllabic <- l / m / n / r
voiced <- b / d / g / j / v / z
unvoiced <- c / f / k / p / s / t / x
l <- comma* [lL] !h !glide !l
m <- comma* [mM] !h !glide !m !z
n <- comma* [nN] !h !glide !n !affricate
r <- comma* [rR] !h !glide !r
b <- comma* [bB] !h !glide !b !unvoiced
d <- comma* [dD] !h !glide !d !unvoiced
g <- comma* [gG] !h !glide !g !unvoiced
v <- comma* [vV] !h !glide !v !unvoiced
j <- comma* [jJ] !h !glide !j !z !unvoiced
z <- comma* [zZ] !h !glide !z !j !unvoiced
s <- comma* [sS] !h !glide !s !c !voiced
c <- comma* [cC] !h !glide !c !s !x !voiced
x <- comma* [xX] !h !glide !x !c !k !voiced
k <- comma* [kK] !h !glide !k !x !voiced
f <- comma* [fF] !h !glide !f !voiced
p <- comma* [pP] !h !glide !p !voiced
t <- comma* [tT] !h !glide !t !voiced
h <- comma* ['h] &nucleus
#___________________________________________________________________
digit <- comma* [0123456789] !h !nucleus
post_word <- pause / !nucleus lojban_word
pause <- comma* space_char+ / EOF
EOF <- comma* !.
comma <- [,]
non_lojban_word <- !lojban_word non_space+
non_space <- !space_char .
space_char <- [.\t\n\r?! ]
#___________________________________________________________________
#spaces <- !Y initial_spaces
#initial_spaces <- (comma* space_char / !ybu Y)+ EOF? / EOF
ybu <- Y space_char* BU
#___________________________________________________________________
#

